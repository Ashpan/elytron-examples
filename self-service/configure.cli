# Batch script to configure Elytron in the WildFly server

# Start batching commands
batch

# Add filesystem realm called exampleFsRealm in the jboss.server.config directory
/subsystem=elytron/filesystem-realm=exampleFsRealm:add(path=fs-realm-users,relative-to=jboss.server.config.dir)

# Add user jane to this filesystem realm
/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity(identity=jane)

# Add attributes to user jane. The attributes consists of Role (User) and some personal information.
/subsystem=elytron/filesystem-realm=exampleFsRealm:set-password(clear={password="passwordJane"}, identity=jane)

/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity-attribute(identity=jane, name=Roles, value=["User"])

/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity-attribute(identity=jane, name=Email, value=["jane@email.com", "jane_email@email.com"])

/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity-attribute(identity=jane, name="Name", value=["Amanda Jane Doe"])

/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity-attribute(identity=jane, name="Phone", value=["123 456 789", "789 456 123"])

/subsystem=elytron/filesystem-realm=exampleFsRealm:add-identity-attribute(identity=jane, name="Location", value=["Prague"])

# Set role decoder to obtain Roles from attribute 'Roles'. Information in this attribute can now be used to restrict access to resources
/subsystem=elytron/simple-role-decoder=from-roles-attribute:add(attribute=Roles)

# Add security domain that uses the above
/subsystem=elytron/security-domain=exampleFsSD:add(realms=[{realm=exampleFsRealm,role-decoder=from-roles-attribute}],default-realm=exampleFsRealm,permission-mapper=default-permission-mapper)

# Set HTTP Basic authentication mechanism for this security domain
/subsystem=elytron/http-authentication-factory=example-fs-http-auth:add(http-server-mechanism-factory=global,security-domain=exampleFsSD,mechanism-configurations=[{mechanism-name=BASIC,mechanism-realm-configurations=[{realm-name=exampleApplicationDomain}]}])

# Add exampleApplicationDomain in undertow and set it to use exampleFsSD security domain created above
/subsystem=undertow/application-security-domain=exampleApplicationDomain:add(security-domain=exampleFsSD)

# Run the batch commands
run-batch

# Reload the server configuration
reload
